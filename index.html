<!doctype html>
<html>

<head>
<title>jFluidic</title>
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1">

<script type="text/javascript" src="glMatrix-0.9.5.min.js"></script>
<script type="text/javascript" src="class.js"></script>
<script type="text/javascript" src="jquery.js"></script>
<script type="text/javascript" src="jfluidic.js"></script>

<script id="inject-fs" type="x-shader/x-fragment">
    precision mediump float;
    
    varying vec2 textureCoord;
    
    uniform vec2 position;
    uniform vec4 velocity;
    uniform sampler2D vectorField;
    
    void main(void) {
        if (length(position - textureCoord) < 0.04) {
            gl_FragColor = velocity;
        } else {
            gl_FragColor = texture2D(vectorField, textureCoord);
        }
    }
</script>

<script id="neighbours-util" type="x-shader/x-fragment">
void F4Neighbours2D(sampler2D tex,
					 vec2 s,
					 out vec4 left,
					 out vec4 right,
					 out vec4 up,
					 out vec4 down)
{
	vec3 p  = vec3(-1, 0, 1)/256.0;
	
	down = texture2D(tex, s + p.yx);
	up = texture2D(tex, s + p.yz);
	left = texture2D(tex, s + p.xy);
	right = texture2D(tex, s + p.zy);
}
</script>

<script id="jacobi-fs" type="x-shader/x-fragment">
    precision mediump float;
    
    varying vec2 textureCoord;
    
    uniform sampler2D x;
    uniform sampler2D b;
    uniform float alpha;
    uniform float beta;
    
    void main(void) {
    	vec4 left, right, up, down;
  
    	vec4 current = texture2D(x, textureCoord);
    	F4Neighbours2D(b, textureCoord, left, right, up, down);
    	
    	gl_FragColor = (current + alpha*(left + right + up + down)) / beta;
    	//gl_FragColor = left;
    }
</script>

<script id="divergence-fs" type="xshader/x-fragment">
    precision mediump float;
    
    varying vec2 textureCoord;
    uniform sampler2D vectorField;
    
    void main() {
    	vec4 left, right, up, down;
    	F4Neighbours2D(vectorField, textureCoord, left, right, up, down);
    	float result  = -0.5*((right.x - left.x) + (up.y - down.y))/256.; // division by 256 really not necessary - why bother?
    	gl_FragColor = vec4(result,0,0,0); // FIXME: Make 1-D
    }
</script>

<script id="subtract-pressure-gradient-fs" type="xshader/x-fragment">
    precision mediump float;    

    varying vec2 textureCoord;
    uniform sampler2D vectorField;
    uniform sampler2D pressure;
    
    void main(void) {
    	vec4 left, right, up, down;    	
      	F4Neighbours2D(pressure, textureCoord, left, right, up, down);
    
    	vec4 result = texture2D(vectorField, textureCoord);
    	result.x -= 0.5 * 256.*(right.x - left.x); // multiplication by 256 really not necessary - why bother?
    	result.y -= 0.5 * 256.*(up.x - down.x);
    	gl_FragColor = vec4(result.xy, 0, 1); // FIXME: Make 1-D
	}
</script>

<script id="bilerp-util" type="x-shader/x-fragment">
vec4 bilerp(sampler2D tex, vec2 s) {
      vec4 st;
      
      s = s * 256.0;
      st.xy = (floor(s - 0.5) + 0.5);
      st.zw = st.xy + 1.0;
      vec2 t = s - st.xy;
      st = st / 256.0;
                
      vec4 tex11 = texture2D(tex, st.xy);
      vec4 tex21 = texture2D(tex, st.zy);
      vec4 tex12 = texture2D(tex, st.xw);
      vec4 tex22 = texture2D(tex, st.zw);
      
      // bilinear interpolation
      return mix(mix(tex11, tex21, t.x), mix(tex12, tex22, t.x), t.y);
}
</script>

<script id="advect-fs" type="x-shader/x-fragment">
    precision mediump float;
    
    varying vec2 textureCoord;
    
    uniform sampler2D affectedField;
    uniform sampler2D vectorField;
    uniform float dt;
    
    void main(void) {
        float speed = 30.0;
        vec4 velocity = texture2D(vectorField, textureCoord);
        vec2 lastPosition = textureCoord - velocity.xy * dt * speed/256.0;
        
        gl_FragColor = bilerp(affectedField, lastPosition);
    }
</script>

<script id="perturb-fs" type="x-shader/x-fragment">

    precision mediump float;
    
    varying vec2 textureCoord;
    
    uniform sampler2D affectedField;
    uniform sampler2D vectorField;
    uniform float dt;
    
    void main(void) {
        
        vec4 velocity = texture2D(vectorField, textureCoord);
        vec4 color =  texture2D(affectedField, textureCoord);
        
        //velocity.x = 1.0;//velocity.x + color.r * dt;
        velocity.y = velocity.y + (color.r - color.b) * dt;
        velocity.a = velocity.y;
        
        gl_FragColor = velocity;
    }
</script>

<script id="draw-fs" type="x-shader/x-fragment">
    precision mediump float;
    
    varying vec4 vColor;
    varying vec2 textureCoord;
    
    uniform sampler2D vectorField;
    uniform float dt;
    
    void main(void) {
        gl_FragColor = vec4(1,0,1,1);
        gl_FragColor = texture2D(vectorField, textureCoord);
        gl_FragColor = abs(gl_FragColor);
    }
</script>
<script id="debug-draw-fs" type="x-shader/x-fragment">
    precision mediump float;
    
    varying vec4 vColor;
    varying vec2 textureCoord;
    
    uniform sampler2D vectorField;
    uniform float dt;
    
    void main(void) {
        //gl_FragColor = vec4(1,0,1,1);
        gl_FragColor = texture2D(vectorField, textureCoord);
        gl_FragColor.a = 1.;
        gl_FragColor = abs(gl_FragColor);
    }
</script>
<script id="shader-vs" type="x-shader/x-vertex">
    attribute vec3 vertices;
    attribute vec2 textureCoords;
    
    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;

    varying vec2 textureCoord;

    void main(void) {
        gl_Position = projectionMatrix * modelViewMatrix * vec4(vertices, 1.0);
        textureCoord = textureCoords;
    }
</script>

<script>

$(function() {
    var canvas = document.getElementById('dest');
    var gl;
    try {
        gl = canvas.getContext('experimental-webgl');
        gl.viewportWidth = canvas.width;
        gl.viewportHeight = canvas.height;
    } catch(e) {}
    
    if (!gl) alert("Could not create gl context - try and use a real browser (that is, Chrome)");
    
    var fluid = new jFluidic.Fluid(gl);
    var interactor = new jFluidic.Interactor($(canvas), fluid);
    
    $(canvas).bind({
        dragover: function() {
            return false; // allows drop
        },
        
        drop: function(event) {
            var dataTransfer = event.originalEvent.dataTransfer;
            var html = event.originalEvent.dataTransfer.getData('text/html');
            if (!html) return false;
            var src = $(html).attr('src');
            
            // Dodgy cross-site fix
            $.get('mirror.php', { src: src }, function(result) {
                fluid.loadImageAsInk(result);
            });
            
            return false;
        }
    });
});

</script>
<style>
    canvas { 
        border: 1px solid white;
        width: 256px; 
        height: 256px; 
    }
    body {
        background: #888;
    }
    img.texture {
        width: 256px; height: 256px;
    }
</style>
</head>
<body>
<h1>jFluidic</h1>
<canvas id="dest" width=256 height=256>
</canvas><br />
<img class="texture" id="ink" title="Ink" />
<img class="texture" id="vectorField" title="Vector Field" />
<img class="texture" id="pressure" title="Pressure" />
<img class="texture" id="divergenceField" title="Divergence" />

<br />
FPS: <span id="fps"></span><br />
Speedup: <input type="number" id="speedup" value="1" />
<br /><label><input type="checkbox" checked="checked" id="go" /> Go</label>
<br /><label><input type="checkbox" checked="checked" id="single-step" /> Single step per frame (~60fps always)</label>
<br /><label><input type="checkbox" achecked="checked" id="debug-ink" /> Debug Ink</label>
<br /><label><input type="checkbox" achecked="checked" id="debug-vectorField" /> Debug Field</label>
<br /><label><input type="checkbox" achecked="checked" id="debug-pressure" /> Debug Pressure</label>
<br /><label><input type="checkbox" achecked="checked" id="debug-divergenceField" /> Debug Div</label>

</body>
</html>